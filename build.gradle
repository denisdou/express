plugins {
    id 'java'
    id 'maven-publish'  // 添加插件
    id 'signing'  // 签名插件
}

group = 'com.github.denisdou'
version = '0.0.6'
sourceCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    mavenLocal()
    maven { url "https://maven.aliyun.com/repository/public" }
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.0'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'javax.activation', name: 'activation', version: '1.1.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '3.0.0'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = "javadoc"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        // 这一个推送项目名称，mavenJava 相当于是一个 task name
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourceJar
            artifact javadocJar

            // 添加 pom 相关信息
            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "express"
                description = "United States Postal Service Web Tool Kit Development for Java applications. The following api will help with the creation and usage of objects when working with usps calls. Also makes it easy to make standard calls to usps、ups services such as validate address, postage rates, and generate labels."
                url = "https://github.com/denisdou/express"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    // 添加开发者描述，这个id不知道是什么
                    developer {
                        id = "denisdou"
                        name = "denisdou"
                        email = "402550833@qq.com"
                    }
                }
                // 添加你的 git 仓库 信息
                scm {
                    connection = "scm:git:https://github.com/denisdou/express.git"
                    developerConnection = "scm:git:https://github.com/denisdou/express.git"
                    url = "https://github.com/denisdou/express"
                }
            }
        }
    }
    repositories {
        // 添加一个远程仓库地址
        // releases 仓库
        maven {
            // 在对 task 中会生成对应的名称 publishMavenJavaPublicationToxxx
            // 后面的 xxx 就是你这里的名称，表示你要把jar 上传到这个仓库中
            name 'sonatypeRepository'  // 为你这个仓库起名
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = "${NEXUS_USERNAME}"  // 之前在 sonatype 注册的账户名
                password = "${NEXUS_PASSWORD}" // 对应的密码
            }
        }
        // snapshots 仓库
        maven {
            name = 'sonatypeSnapshotRepository'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
        //阿里云仓库
        maven {
            name = 'aliYunRepository'
            url = project.version.toString().endsWith('-SNAPSHOT')?
                    "https://repo.rdc.aliyun.com/repository/133896-snapshot-njEnrt/"
                    :"https://repo.rdc.aliyun.com/repository/133896-release-3W4XuY/"
            //认证用户和密码
            credentials {
                username 'EvRr7H'
                password 'u8LYSo6nCs'
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    // <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}